apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

if (hasProperty("disableCryFS")) {
    ext.disableCryFS = getProperty("disableCryFS")
} else {
    ext.disableCryFS = false
}

if (hasProperty("disableGocryptfs")) {
    ext.disableGocryptfs = getProperty("disableGocryptfs")
} else {
    ext.disableGocryptfs = false
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"
    ndkVersion "25.1.8937393"
    namespace "sushi.hardcore.droidfs"

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "sushi.hardcore.droidfs"
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 30
        versionCode 29
        versionName "2.0.0-alpha2"

        ndk {
            abiFilters "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
        }

        externalNativeBuild.cmake {
            if (project.ext.disableGocryptfs) {
                arguments "-DGOCRYPTFS=OFF"
            }
            if (project.ext.disableCryFS) {
                arguments "-DCRYFS=OFF"
            }
        }
    }

    if (!hasProperty("nosplits")) {
        splits {
            abi {
                enable true
                universalApk true
            }
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
        buildConfigField "boolean", "CRYFS_DISABLED", "${project.ext.disableCryFS}"
        buildConfigField "boolean", "GOCRYPTFS_DISABLED", "${project.ext.disableGocryptfs}"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode true
                proguardFiles 'proguard-rules.pro'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }
}

dependencies {
    implementation project(":libpdfviewer:app")
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

    implementation "androidx.sqlite:sqlite-ktx:2.3.0"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'com.google.android.material:material:1.8.0'
    implementation "com.github.bumptech.glide:glide:4.13.2"
    implementation "androidx.biometric:biometric-ktx:1.2.0-alpha05"

    def exoplayer_version = "2.18.2"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"

    implementation "androidx.concurrent:concurrent-futures:1.1.0"

    def camerax_version = "1.2.0-beta02"
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"
    implementation "androidx.camera:camera-extensions:$camerax_version"
}
